buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        if (project.hasProperty('coverage')) {
            classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.3.1'
        }
        classpath "com.ofg:micro-common-release:0.1.17"
    }
    dependencies {
        ant.unjar src: configurations.classpath.find { it.name.startsWith("micro-common-release") }, dest: 'build/release'
    }
}

apply from: "${rootProject.buildDir}/release/gradle/version.gradle"
scmVersion {
    tag {
        prefix = ''
    }
}

project.group = 'com.ofg'
project.version = scmVersion.version
project.description = 'Gradle plugin that tells you what libs have new versions in Maven Central / JCenter'

apply plugin: 'groovy'
apply from: "${rootProject.buildDir}/release/gradle/publish.gradle"
apply from: "${rootProject.buildDir}/release/gradle/release.gradle"

bintray {
    pkg {
        repo = 'uptodate-gradle-plugin'
        labels = ['gradle']
        version {
            attributes = ['gradle-plugin': 'com.ofg.uptodate:com.ofg:uptodate-gradle-plugin']
        }
    }
}

sourceCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile("org.codehaus.groovy.modules.http-builder:http-builder:0.7.1") {
        exclude group: 'xerces', module: 'xercesImpl' //conflicts with xercesImpl from Gradle (in functional tests)
    }

    testCompile('org.spockframework:spock-core:1.0-groovy-2.3') {
        exclude group: 'org.codehaus.groovy'    //to not override version from Gradle
    }
    testCompile 'com.github.tomakehurst:wiremock:1.50'  //1.51+ requires httpclient 4.3+ which conflicts with version distributed with Gradle
    testRuntime 'cglib:cglib-nodep:2.2.2'
    testRuntime 'org.objenesis:objenesis:1.2'
}

wrapper {
    gradleVersion '2.3'
}

bintrayUpload.dependsOn 'build'

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

if (project.hasProperty('coverage')) {
    apply plugin: 'jacoco'
    apply plugin: 'com.github.kt3k.coveralls'

    jacoco {
        toolVersion = '0.7.4.201502262128'
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }
}

